version=2
:if:<camera.thirdperson>=true
	@storage.set(bool,SetToFirstPerson,false)
:else
	@storage.set(bool,SetToFirstPerson,true)
:endif

@camera.activatethirdperson
@player.turnto(2)
@camera.setyaw(0)
@camera.reset
@camera.setpitch(-0.20)
@level.wait(10)
@camera.fix
@sound.play(Teleport)

@Player.SetMovement(0,1,0)
@Player.SetSpeed(3)
@Player.Move(3)

@screen.fadeoutcolor(255,255,255)
@screen.fadeout(15)
@camera.update
:select:<level.filename>
	:when:UndergroundCaveMaze_B1F
		@player.warp(FinalDungeon\WarpRoom.dat,1,0.1,1)
	:when:SmallRoom4
		@player.warp(FinalDungeon\WarpRoom.dat,1,0.1,3)
	:when:SmallRoom5
		@player.warp(FinalDungeon\WarpRoom.dat,5,0.1,5)
	:when:SmallCave1
		@player.warp(FinalDungeon\WarpRoom.dat,5,0.1,1)
	:when:SmallRoom6
		@player.warp(FinalDungeon\WarpRoom.dat,5,0.1,3)
	:when:Lobby
		@player.warp(FinalDungeon\WarpRoom.dat,3,0.1,5)
	:when:WarpRoom
		:select:<player.position>
			:when:1,3,1
				@player.warp(FinalDungeon\UndergroundCaveMaze_B1F.dat,17,0.1,21)
			:when:1,3,3
				@player.warp(FinalDungeon\SmallRoom4.dat,1,0.1,1)
			:when:1,3,5
				@player.warp(FinalDungeon\WarpRoom.dat,3,0.1,1)
			:when:3,3,1
				@player.warp(FinalDungeon\WarpRoom.dat,1,0.1,5)
			:when:3,3,5
				@player.warp(FinalDungeon\Lobby.dat,5,0.1,0)
			:when:5,3,1
				@player.warp(FinalDungeon\SmallCave1.dat,3,0.1,7)
			:when:5,3,3
				@player.warp(FinalDungeon\SmallRoom6.dat,2,0.1,2)
			:when:5,3,5
				@player.warp(FinalDungeon\SmallRoom5.dat,2,0.1,4)
		:endwhen
:endwhen

@level.update
@camera.defix
@camera.update
@camera.fix
@player.warp(~,2.9,~)
@level.update
@screen.fadein(15)
@camera.update

@Player.SetMovement(0,-1,0)
@Player.SetSpeed(3)
@Player.Move(3)

@camera.defix
@screen.fadeoutcolor
:if:<storage.get(bool,SetToFirstPerson)>=true
	@Camera.DeactivateThirdPerson
:endif
:end